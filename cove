#! /usr/bin/fish

# Variables that'll depend on installation
set repo_dir ~/Documents/bib

# Variables that are expected to stay constant across installations
set key_format ~/.config/cove/key_format.rsc # where to store resource file for bibtool key formatting
set fzf_format ~/.config/cove/fzf_format.rsc
set repo $repo_dir/repo.bib
set doi_list $repo_dir/doi
set fzf_list $repo_dir/.fzf
set new_bibs /tmp/cove_new.bib
set temp_repo /tmp/cove_temp.bib

# set default behaviour for no argument
if ! count $argv > /dev/null
  set argv open
end

# set behaviour for core commands
switch $argv[1]
  case ""
    echo here!
  exit
  case cite
    echo -e (cat $fzf_list) | fzf --ansi | awk 'NF{ print $NF }'
    exit
  case title
    echo -e (cat $fzf_list) | fzf --ansi | sed 's/.*[0-9]\.//' | sed 's/\. \[.*//'
    exit
  case link
    if ! echo $argv[2] | grep -q '\.pdf$'
      echo 'Please supply a valid pdf!'
      exit
    end
    set pdf_path (echo -e (cat $fzf_list) | fzf --ansi | awk 'NF{ print $NF }')
    mv $argv[2] $repo_dir/papers/$pdf_path.pdf
    exit
  case unlink
    set pdf_path (echo -e (cat $fzf_list) | fzf --ansi | awk 'NF{ print $NF }')
    rm -f $repo_dir/papers/$pdf_path.pdf
    exit
  case open
    set pdf_path (echo -e (cat $fzf_list) | fzf --ansi | awk 'NF{ print $NF }')
    if test -f $repo_dir/papers/$pdf_path.pdf
      zathura $repo_dir/papers/$pdf_path.pdf &
    else
      echo 'PDF not found! Are you sure you linked it?'
    end
    exit
  end

# Otherwise treat args as a list of doi's

# check for doi's already in repo
for doi in $argv
  if grep -xq $doi $doi_list
    echo One or more entries already in repo!
    exit
  end
end

# Download bibtex records
# -s = silent; -L = redirect location; -H = add custom header to request
rm -f $new_bibs
for doi in $argv
  curl -sLH 'Accept: application/x-bibtex' http://dx.doi.org/$doi >> $new_bibs
end

# Check for broken doi links
if grep 'DOI Not Found' $new_bibs
  echo One or more invalid DOIs found!
  exit
end

# Update doi list
echo $argv | sed 's/ /\n/g' >> $doi_list

# manage key with bibtool
rm -f $temp_repo
bibtool -r $key_format $repo $new_bibs | cat > $temp_repo
mv $temp_repo $repo

# generate file for fzf searching
bibtool -r $fzf_format $repo | grep '^@' | sed 's/.*{\s*/\\\\n/' | sed 's/[+,]/ /g' | sed 's/+/,/g' | sed 's/e\[/\\\\e[/g' > $fzf_list
